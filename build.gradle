apply plugin: 'com.android.library'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'kotlin-android'

buildscript {
    ext.retrofit_version = '2.3.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'de.mobilej.unmock:UnMockPlugin:0.6.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven {
        url "http://repository.jetbrains.com/all" // source of Spek
    }
}

android {
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion target_sdk_version
        versionCode 2
        // The versionCode to which the current version's cache is backwards compatible with. Update after breaking changes that require a cache reset.
        buildConfigField "int", "CACHE_COMPATIBILITY", "2"
        versionName "2.0.0"

        // in this files proguard rules are exposed and can be used by the application project
        consumerProguardFiles 'proguard-library-consumer.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    api 'androidx.legacy:legacy-support-v4:1.0.0' // required for FileProvider and Annotations

    // ReactiveX
    api 'io.reactivex.rxjava2:rxandroid:2.1.1' // rxjava (observables)
    api 'io.reactivex.rxjava2:rxjava:2.2.11' // RxAndroid releases are few and far between, so depend on latest version for bug fixes and new features

    // network
    api "com.squareup.retrofit2:retrofit:$retrofit_version" // http requests
    api "com.squareup.retrofit2:converter-gson:$retrofit_version" // json parsing
    api "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version" // retrofit with observables
    api 'com.squareup.picasso:picasso:2.71828' // ion provides a picasso wrapper adding an authorization header to requests
    api 'androidx.exifinterface:exifinterface:1.2.0' // ensure picasso's dependency to support library is up-to-date

    // misc
    implementation 'org.apache.commons:commons-compress:1.12'
    api 'joda-time:joda-time:2.10.2' // better DateTime handling

    testApi 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testApi "org.jetbrains.spek:spek:1.0.25"
    testApi "nl.jqno.equalsverifier:equalsverifier:3.1.9"

    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

// allow unit tests to use some basic android library classes
unMock {
    // URI to download the android-all.jar from. e.g. https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/
    downloadFrom 'https://oss.sonatype.org/content/groups/public/org/robolectric/android-all/4.3_r2-robolectric-0/android-all-4.3_r2-robolectric-0.jar'

    keep "android.net.Uri"
    keepStartingWith "android.text."
    keepStartingWith "org.ccil.cowan.tagsoup."
    keepStartingWith "libcore."
    keepStartingWith "com.android.internal.util."
    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}

// output in gradle console
println("********************************** ------------------ **********************************")
println("debugging: environment variable JAVA8_HOME must be set and point to Java 8 home folder")
println("JAVA8_HOME: " + System.getenv("JAVA8_HOME"))
println("********************************** ------------------ **********************************")
