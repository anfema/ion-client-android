buildscript {
    ext.kotlin_version = '1.6.21'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.bjoernq:unmockplugin:0.7.9'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

apply plugin: 'de.mobilej.unmock'

android {
    namespace 'com.anfema.ionclient'

    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        // The versionCode to which the current version's cache is backwards compatible with. Update after breaking changes that require a cache reset.
        buildConfigField "int", "CACHE_COMPATIBILITY", "3"

        // in this files proguard rules are exposed and can be used by the application project
        consumerProguardFiles 'proguard-library-consumer.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'

    // ReactiveX
    api 'io.reactivex.rxjava2:rxandroid:2.1.1' // rxjava (observables)
    api 'io.reactivex.rxjava2:rxjava:2.2.21' // RxAndroid releases are few and far between, so depend on latest version for bug fixes and new features

    // network
    def retrofitVersion = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion" // http requests
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion" // json parsing
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion" // retrofit with observables
    def okHttpVersion = "4.9.3"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // misc
    implementation 'org.apache.commons:commons-compress:1.20'
    api 'net.danlew:android.joda:2.10.6' // better DateTime handling
    testImplementation "joda-time:joda-time:2.10.10" // Time zone db for unit tests

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation "org.jetbrains.spek:spek-api:1.0.89"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:3.1.9"

    androidTestImplementation 'androidx.annotation:annotation:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'

    unmock 'org.robolectric:android-all:4.3_r2-robolectric-0'
}

// allow unit tests to use some basic android library classes
unMock {
    keep "android.net.Uri"
    keepStartingWith "android.text."
    keepStartingWith "org.ccil.cowan.tagsoup."
    keepStartingWith "libcore."
    keepStartingWith "com.android.internal.util."
    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}
